apply plugin: 'application'
apply plugin: 'maven-publish'
mainClassName = 'com.mesosphere.dcos.cassandra.executor.Main'

ext {
    junitVer = '4.12'
    datastaxVer = '3.1.0'
    awsVer = '1.11.49'
}

dependencies {
    compile project(':cassandra-commons')
    compile ('com.google.guava:guava:18.0'){force = true}
    compile 'org.apache.cassandra:cassandra-all:3.0.9'
    compile 'org.apache.commons:commons-compress:1.5'
    compile ("com.amazonaws:aws-java-sdk-core:${awsVer}"){force = true}
    compile "com.amazonaws:aws-java-sdk:${awsVer}"
    compile "com.microsoft.azure:azure-storage:4.2.0"
    compile "commons-io:commons-io:2.5"
    compile "com.datastax.cassandra:cassandra-driver-core:${datastaxVer}"

    compile 'org.xerial.snappy:snappy-java:1.1.2.1'
    // To override the junit compile dependency that cassandra brings in.
    compile "junit:junit:${junitVer}"
    testCompile "junit:junit:${junitVer}"
}

// build.sh needs executor.zip for ci_upload.py
task copyDist(type: Copy) {
    dependsOn distTar, distZip
    from "$buildDir/distributions/"
    into "$buildDir/distributions/"
    rename("${project.name}-${project.version}", "executor")
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact("$buildDir/distributions/${project.name}-${project.version}.zip") {
                extension 'zip'
            }
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv('REPOSITORY_USERNAME')
                password System.getenv('REPOSITORY_PASSWORD')
            }
            url System.getenv('REPOSITORY_URL') ?: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

publishToMavenLocal.dependsOn(build)
publish.dependsOn(build)

build.dependsOn(copyDist)
